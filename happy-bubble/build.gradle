apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}


task androidSourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.source
    exclude "**/R.class"
    exclude "**/BuildConfig.class"
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

publishing {
    // å®šä¹‰å‘å¸ƒä»€ä¹ˆ
    publications {
        mavenJava(MavenPublication) {
            // group idï¼Œå‘å¸ƒåå¼•ç”¨çš„ä¾èµ–çš„ group id
            groupId 'com.github.xujiaji'
            // å‘å¸ƒåå¼•ç”¨çš„ä¾èµ–çš„ artifact id
            artifactId 'happy-bubble'
            // å‘å¸ƒçš„ç‰ˆæœ¬
            version android.defaultConfig.versionName
            // å‘å¸ƒçš„ arr çš„æ–‡ä»¶å’Œæºç æ–‡ä»¶
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            artifact androidSourcesJar
            pom {
                // æ„ä»¶åç§°ï¼Œå¯ä»¥è‡ªå®šä¹‰
                name = 'HappyBubble'
                // æ„ä»¶æè¿°
                description = 'ğŸ’­BubbleLayoutéšæ„å˜åŒ–çš„æ°”æ³¡å¸ƒå±€ã€æ¶ˆæ¯å¯¹è¯æ¡†ï¼Œå¯å®šåˆ¶é¢œè‰²ï¼ŒèƒŒæ™¯ã€å¼§åº¦ã€å°–è§’å¼§åº¦ã€è¾¹æ¡†ç­‰ç­‰ã€‚BubbleDialogæ°”æ³¡å¼¹çª—æ ¹æ®ç‚¹å‡»Viewçš„ä½ç½®å®šä½å®ƒçš„å¼¹çª—ä½ç½®ï¼ŒBubbleDialogå¯å®šåˆ¶æ–¹å‘ç­‰ï¼(BubbleLayout changes freely,BubbleDialog click on the location of View positioning its location,BubbleDialog can be customized directions.)'
                // æ„ä»¶ä¸»é¡µ
                url = 'https://github.com/xujiaji/HappyBubble'
                // è®¸å¯è¯åç§°å’Œåœ°å€
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                // å¼€å‘è€…ä¿¡æ¯
                developers {
                    developer {
                        name = 'xujiaji'
                        email = 'jiajixu@qq.com'
                    }
                }
                // ç‰ˆæœ¬æ§åˆ¶ä»“åº“åœ°å€
                scm {
                    url = 'https://github.com/xujiaji/HappyBubble'
                    connection = 'https://github.com/xujiaji/HappyBubble.git'
                    developerConnection = 'git@github.com:xujiaji/HappyBubble.git'
                }
                // è§£å†³ä¾èµ–å…³ç³»
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    project.configurations.all { configuration ->
                        def name = configuration.name
                        if (name != "implementation" && name != "compile" && name != "api") {
                            return
                        }
                        println(configuration)
                        configuration.dependencies.each {
                            println(it)
                            if (it.name == "unspecified") {
                                // å¿½ç•¥æ— æ³•è¯†åˆ«çš„
                                return
                            }
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                            if (name == "api" || name == "compile") {
                                dependencyNode.appendNode("scope", "compile")
                            } else { // implementation
                                dependencyNode.appendNode("scope", "runtime")
                            }
                        }
                    }
                }
            }
        }
    }
    // å®šä¹‰å‘å¸ƒåˆ°å“ªé‡Œ
    repositories {
        maven {
            // å‘å¸ƒçš„ä½ç½®ï¼Œè¿™é‡Œæ ¹æ®å‘å¸ƒçš„ç‰ˆæœ¬åŒºåˆ†äº† SNAPSHOT å’Œæœ€ç»ˆç‰ˆæœ¬ä¸¤ç§æƒ…å†µ
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                // è¿™é‡Œå°±æ˜¯ä¹‹å‰åœ¨ issues.sonatype.org æ³¨å†Œçš„è´¦å·
                username properties.getProperty("maven.username")
                password properties.getProperty("maven.password")
            }
        }
    }
}

signing {
    sign publishing.publications
}